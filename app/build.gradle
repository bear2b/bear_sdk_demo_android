import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext.supportLibs_version = '28.0.0'
ext.bearsdk_version = '3.3.0'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.bear2b.sampleapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode getTimeStamp()
        versionName bearsdk_version + "-" + getTimeStamp()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    kapt {
        generateStubs = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "lang"
    productFlavors {
        java {
            dimension "lang"
        }
        kotlin {
            dimension "lang"
            versionNameSuffix "-kt"
            applicationIdSuffix ".kt"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'inputList.txt'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation("com.bear:bearsdk:$bearsdk_version@aar") {
        transitive = true
    }

    // FireBase
    implementation "com.google.firebase:firebase-core:16.0.9"
    implementation "com.google.firebase:firebase-messaging:18.0.0"

    // Support
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.0.0'

    // Runtime Permissions
    implementation 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation 'nl.littlerobots.rxlint:rxlint:1.7.4'

    kotlinImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

static def getTimeStamp() {
    def df = new SimpleDateFormat("yyMMddHHmm")
    return df.format(new Date()).toInteger()
}

apply plugin: 'com.google.gms.google-services'
